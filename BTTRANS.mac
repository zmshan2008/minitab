GMACRO
BTTRANS

#**************************************************************************
#
# Macro:             BTTRANS.MAC
# Final MTB Date:    12/05/04
# First MTB Date:    03/10/94
# First Date:        06/22/82 (APL Code)
# Author:            Mike Delozier
#                    Minitab Inc.
#                    1829 Pine Hall Road
#                    State College, PA  16801
#
#**************************************************************************
#
#  This macro executes the Box-Tidwell procedure to determine appropriate 
#  predictor variable power transformations for a regression model linear 
#  in the transformed predictors.  It is important to note that this 
#  procedure can be numerically unstable resulting in error conditions for 
#  some data sets.
#
#  Before running the macro, you must display the command prompt, MTB>, in 
#  the  Session window.  To display the command prompt, click anywhere in 
#  the Session window and choose Editor > Enable Commands. MTB> will now 
#  be displayed at the bottom of the Session Window.  
#
#  To launch the macro, at the MTB> prompt in the Session window, type
#  
#  %BTTRANS
#
#  Press Enter.  
#
#  The macro will ask you for the number of predictor variables in the 
#  analysis, where these predictors are stored, and where the response 
#  variable is stored.  The macro will ask you for the value of the power 
#  transformation parameter used on the response variable.  
#
#**************************************************************************
#
#  Neither Minitab, Inc. nor the author(s) of this MACRO makes any claim 
#  of or offers any Warranty whatsoever with regard to the accuracy of 
#  this MACRO or its suitability for use.  Minitab, Inc. and the author(s)
#  of this MACRO each hereby disclaims any Warranty and/or liability with 
#  respect thereto. 
#
#**************************************************************************

mreset
notitle
noecho
brief 2 

note
note Please enter the number of predictor variables in the analysis...
set c509;
  file 'terminal';
  nobs 1.
copy c509 k157

do k200 = 1:k157
  note
  note Please enter column number of predictor variable...
  set c510;
    file 'terminal';
    nobs 1.
  copy c510 k159
  let k160 = 520+k200-1
  let ck160 = ck159
  let k162 = 540+k200-1
  let ck162 = ck159
  let k173 = 540+k157+k200-1
  let ck173 = ck162*loge(ck162)
enddo

note
note Please enter column number of response variable...
set c509;
  file 'terminal';
  nobs 1.
copy c509 k161
let c505 = ck161
note
note Please enter response power transformation parameter value...
set c509;
  file 'terminal';
  nobs 1.
copy c509 k300

if k300 eq 0
  let c505 = loge(c505)
else
  let c505 = c505**k300
endif

let k175 = 2*k157
let k177 = k157+1
set c500
  k157(1)
end
note
note
note BOX-TIDWELL POWER TRANSFORMATION PROCEDURE ...
note
note ~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~
note
do k174 = 1:3
  brief 0
  regress c505 k157 c540-ck162;  
    coefficients c501.
  regress c505 k175 c540-ck173;
    tolerance .00001;
    coefficients c502.
  copy c501 c501;
    omit 1.
  copy c502 c502;
    omit 1:k177.
  let c503 = (c502/c501)+1
  let c503 = c500*c503
  let c500 = c503
  brief 2
  note ITERATION...
  write k174
  note
  note ESTIMATED ALPHA(S)...
  write c503
  note ~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~
  note
  do k176 = 1:k157
    let k160 = 520+k176-1
    let k162 = 540+k176-1
    let k163 = c503(k176)
    let k172 = absolute(k163) lt .01
    if k172 eq 1
      let ck162 = loge(ck160)
    else
      let ck162 = ck160**k163
    endif
    let k173 = 540+k157+k176-1
    let ck173 = ck162*loge(ck162)
  enddo
enddo
ENDMACRO